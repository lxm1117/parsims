TITLE Naf2 channel
: 05Apr10  modified to add Nav1.6 channel with similar properties to Naf
:
:
: Apr08 Added a third particle s for slow inactivation
:	sfrac is the gbar fraction to assign to s particle
:	ie, g = gbar*n^n_gates*h*(1-sfrac) + gbar*n^n_gates*h*s*sfrac	
:
: Jose Ambros-Ingerson jose@kiubo.net 2006

NEURON {
  SUFFIX 	Naf2_i1
  USEION 	na READ ena WRITE ina
  RANGE		gbar, g, i,gbar16, g2

  GLOBAL 	inf_n, tau_n, inf_h, tau_h
  GLOBAL 	inf_n2, tau_n2, inf_h2, tau_h2
}

PARAMETER {
  ena 			(mV)
  gbar		= 1000 	(pS/um2)
  gbar16	= 10 	(pS/um2)

  : inf_n parameters
  gates_n	= 3
  vhalf_n	= -30	(mV)	
  slope_n	= -7	(mV)

  : inf_n2 parameters
  vhalfD_n2	= -10	(mV)		: Vhalf is less depolarized than for n	

  : tau_n parameters
  tauA_n	= 0.5 	(ms)
  tauDv_n	= 0	(mV)	: Delta to vhalf_n
  tauG_n	= 0.5		: Left-right bias. range is (0,1)
  tauF_n	= 0		: Up-Down bias. range is ~ -3.5(cup-shape), -3(flat), 0(from k), 1(sharper)
  tau0_n	= 0.001	(ms)	: minimum tau

  : inf_h parameters
  vhalfD_h	= -25   (mV)	:  vhalf_h = vhalf_n + vhalfD_h; 
  slope_h	= 8	(mV)

  : inf_h2 parameters
  vhalfD_h2	= -25   (mV)	:  vhalf_h2 = vhalf_n + vhalfD_n + vhalfD_h2; 

  : tau_h parameters
  tauA_h	= 20 	(ms)
  tauDv_h	= 0	(mV)	: Delta to vhalf_h
  tauG_h	= 0.5		: Left-right bias. range is (0,1)
  tauF_h	= 0		: Up-Down bias. range is ~ -3.5(cup-shape), -3(flat), 0(from k), 1(sharper)
  tau0_h	= 0.01	(ms)	: minimum tau
}

STATE {
  n	: activation
  h	: inactivation
  n2	: activation nav 1.6
  h2	: inacivation nav 1.6
}

ASSIGNED {
  v		(mV)
  celsius	(degC)
  ina 		(mA/cm2)
  g		(pS/um2)
  g2		(pS/um2)
  i		(mA/cm2)
  inf_n
  inf_n2
  tau_n		(ms)
  tau_n2	(ms)
  inf_h
  inf_h2
  tau_h		(ms)
  tau_h2	(ms)
}

BREAKPOINT {
  SOLVE states METHOD cnexp
  g = 0
  g2 = 0
  if( n >=0 ){	: make sure no domain error for pow. Cvode may test it
    g 	= gbar*n^gates_n*h
  }
  if( n2 >=0 ){	: make sure no domain error for pow. Cvode may test it
    g2 	= gbar16*n2^gates_n*h2
  }
  i	= g * ( v - ena ) * (1e-4) + g2 * (v - ena ) * (1e-4)
  ina	= i
}

INITIAL {
  rates( v )
  n = inf_n
  h = inf_h
  n2 = inf_n2
  h2 = inf_h2
}

UNITS {
  (mA)	= (milliamp)
  (mV)	= (millivolt)
  (pS)	= (picosiemens)
  (um)	= (micrometer)
}

DERIVATIVE states {     : exact when v held constant; integrates over dt step
  rates( v )
  n' = ( inf_n - n )/ tau_n
  h' = ( inf_h - h )/ tau_h
  n2' = ( inf_n2 - n2 )/ tau_n2
  h2' = ( inf_h2 - h2 )/ tau_h2
}

PROCEDURE rates( v (mV)){
  inf_n = Boltzmann( v, vhalf_n, slope_n )
  tau_n = BorgMod_tau( v, vhalf_n, slope_n, tau0_n, tauA_n, tauG_n, tauF_n, tauDv_n )

  inf_h = Boltzmann( v, vhalf_n + vhalfD_h, slope_h )
  tau_h = BorgMod_tau( v, vhalf_n + vhalfD_h, slope_h, tau0_h, tauA_h, tauG_h, tauF_h, tauDv_h )

  inf_n2 = Boltzmann( v, vhalfD_n2+vhalf_n, slope_n )
  tau_n2 = BorgMod_tau( v, vhalf_n + vhalfD_n2, slope_n, tau0_n, tauA_n, tauG_n, tauF_n, tauDv_n )

  inf_h2 = Boltzmann( v, vhalf_n + vhalfD_n2 + vhalfD_h2, slope_h )
  tau_h2 = BorgMod_tau( v, vhalf_n + vhalfD_n2 + vhalfD_h2, slope_h, tau0_h, tauA_h, tauG_h, tauF_h, tauDv_h )
}

FUNCTION Boltzmann( v (mV), v5 (mV), s (mV) ){
  Boltzmann = 1 / (1 + exp( (v - v5) / s ))
}

FUNCTION BorgMod_tau( v (mV), v5 (mV), s (mV), tau0 (ms), tauA (ms), tauG, tauF, tauDv (mV) ) (ms) {
  LOCAL kc, kr, Dv, wr

  kr = 1000
  wr = 1000
  Dv = (v - ( v5 + tauDv )) * 1(1/mV)
  kc = kr * 10^tauF / s * 1(mV)

  BorgMod_tau = tau0 + tauA * 2 * sqrt( tauG * (1-tauG))
		* ( exp( - Dv *tauG/wr/kc ) + exp( Dv *(1-tauG)/wr/kc ))
 	        / ( exp( - Dv *(1-tauG)*kc/kr ) + exp( Dv *kc*tauG/kr ))
}





